plugins {
    id 'java-library'
    id 'scala'
    id 'maven-publish'
    id 'signing'
}

group 'com.lucendar'
version '4.0.0-SNAPSHOT'

compileJava.options.encoding = 'UTF-8'
compileScala.options.encoding = 'UTF-8'

sourceCompatibility = 17
targetCompatibility = 17

repositories {
    mavenCentral()
}

dependencies {
    implementation('org.scala-lang:scala-library:2.13.12')
    implementation('com.typesafe.scala-logging:scala-logging_2.13:3.9.2')

    implementation('org.checkerframework:checker-qual:3.36.0')
    implementation('com.google.code.gson:gson:2.10.1')
    implementation("com.squareup.okhttp3:okhttp:4.11.0")
    implementation("com.squareup.okhttp3:logging-interceptor:4.11.0")
    implementation("org.slf4j:slf4j-api:2.0.7")

    implementation("org.springframework:spring-webmvc:6.0.10")
    implementation("org.springframework:spring-websocket:6.0.10")
    implementation("org.springframework:spring-messaging:6.0.10")


    compileOnly 'jakarta.servlet:jakarta.servlet-api:6.0.0'

    implementation('io.netty:netty-all:4.1.104.Final')

    implementation('commons-codec:commons-codec:1.15')
    implementation('org.apache.commons:commons-lang3:3.11')
    implementation('commons-validator:commons-validator:1.7')
    implementation('dnsjava:dnsjava:3.4.2')


    implementation("com.lucendar:lucendar-common:2.2.0-SNAPSHOT")
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java

            pom {
                name = 'GT-Streaming SDK library'
                packaging = 'jar'
                // optionally artifactId can be defined here
                description = 'A common library used in GT-Streaming application.'
                url = 'https://github.com/kwankin-yau/strm-sdk'

                scm {
                    connection = 'https://github.com/kwankin-yau/strm-sdk.git'
                    developerConnection = 'https://github.com/kwankin-yau/strm-sdk.git'
                    url = 'https://github.com/kwankin-yau/strm-sdk'
                }

                licenses {
                    license {
                        name = 'MIT'
                        url = 'http://opensource.org/licenses/MIT'
                    }
                }

                developers {
                    developer {
                        id = 'kky'
                        name = 'kwankin-yau'
                        email = 'alphax@vip.163.com'
                    }
                }
            }
        }
    }
}

javadoc {
    options.encoding = 'UTF-8'

    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

signing {
    sign publishing.publications.maven
}

